# Copyright 2024 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//src/roma/gvisor/config:container.bzl", "roma_container_dir", "roma_container_root_dir")

pkg_files(
    name = "gvisor_execs",
    srcs = select({
        "@platforms//cpu:arm64": [
            "@gvisor_containerd_arm64//file",
            "@gvisor_runsc_arm64//file",
        ],
        "@platforms//cpu:x86_64": [
            "@gvisor_containerd_amd64//file",
            "@gvisor_runsc_amd64//file",
        ],
    }),
    attributes = pkg_attributes(mode = "0555"),
    prefix = "/usr/bin",
)

pkg_tar(
    name = "gvisor_tar",
    srcs = [
        ":gvisor_execs",
    ],
    visibility = ["//src/roma/gvisor:__subpackages__"],
)

cc_library(
    name = "grpc_client",
    srcs = ["grpc_client.cc"],
    hdrs = ["grpc_client.h"],
    visibility = ["//src/roma/gvisor:__subpackages__"],
    deps = [
        "//src/core/common/uuid",
        "//src/roma/gvisor/interface:roma_api_grpc_proto",
        "//src/util/status_macro:status_macros",
        "//src/util/status_macro:status_util",
        "@com_google_absl//absl/log",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/status:statusor",
    ],
)

pkg_files(
    name = "gvisor_server_execs",
    srcs = [
        "//src/roma/gvisor/container/sandbox:gvisor_server",
    ],
    attributes = pkg_attributes(mode = "0555"),
    prefix = "/server/bin",
)

pkg_tar(
    name = "gvisor_server_tar",
    srcs = [
        ":gvisor_server_execs",
    ],
)

oci_image(
    name = "gvisor_server_image",
    base = select({
        "@platforms//cpu:aarch64": "@runtime-ubuntu-fulldist-debug-root-arm64",
        "@platforms//cpu:x86_64": "@runtime-ubuntu-fulldist-debug-root-amd64",
    }),
    tars = [
        ":gvisor_server_tar",
    ],
)

_server_image = "bazel/src/roma/gvisor_server:v1"

oci_tarball(
    name = "gvisor_server_image_tarball",
    image = ":gvisor_server_image",
    repo_tags = [_server_image],
)

genrule(
    name = "gen_gvisor_server_container_tar",
    srcs = [
        ":gvisor_server_image_tarball",
        ":config.json",
    ],
    outs = [
        "gvisor_server_container.tar",
    ],
    cmd_bash = """
rm -rf "{roma_container_dir}"
docker load -i "$(location :gvisor_server_image_tarball)"
docker create --privileged --name "{container_name}" "{image_uri}"
mkdir "{roma_container_dir}"
cp "$(location config.json)" "{roma_container_dir}"/
mkdir --mode=755 "{roma_container_dir}"/"{roma_container_root_dir}"
docker export "{container_name}" \
  | tar -xf - -C "{roma_container_dir}"/"{roma_container_root_dir}"/ --same-owner --same-permissions
tar cfz $@ "{roma_container_dir}"/
docker rm "{container_name}"
""".format(
        container_name = "gvisor_server_container",
        image_uri = _server_image,
        roma_container_dir = roma_container_dir,
        roma_container_root_dir = roma_container_root_dir,
    ),
    visibility = ["//visibility:public"],
)
